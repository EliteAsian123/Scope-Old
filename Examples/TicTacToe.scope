import Standard/Core;
import Standard/ANSI;

auto turn = "X";
int turnNum = 0;

auto board = new string[9] with " ";

func void printBoard() {
	print(Ansi.reset + Ansi.clearScreen + Ansi.curHome);
	println("");
	println(" " + board[0] + "|" + board[1] + "|" + board[2] + "    " + "1|2|3");
	println(" -----    -----");
	println(" " + board[3] + "|" + board[4] + "|" + board[5] + "    " + "4|5|6");
	println(" -----    -----");
	println(" " + board[6] + "|" + board[7] + "|" + board[8] + "    " + "7|8|9");
	println("");
}

func string getWinner(string[] nboard) {
	func bool eq(int a, int b, int c) {
		ret nboard[a] != " " && nboard[a] == nboard[b] && nboard[a] == nboard[c];
	}
	
	// Check rows
	for (int i = 0, i < 3, i++) {
		if (eq(i * 3, i * 3 + 1, i * 3 + 2)) {
			ret nboard[i * 3];
		}
	}
	
	// Check columns 
	for (int i = 0, i < 3, i++) {
		if (eq(i, i + 3, i + 6)) {
			ret nboard[i];
		}
	}
	
	// Check diagonals
	if (eq(0, 4, 8)) {
		ret nboard[0];
	}
	if (eq(2, 4, 6)) {
		ret nboard[2];
	}
	
	ret " ";
}

func void doTurn(bool isInvalid) {
	printBoard();
	
	if (isInvalid) {
		println("Invalid spot!");
	} else {
		println("");
	}
	print("Where you would you like to go " + turn + "? " + Ansi.bold);
	
	string in = input();
	int index = atoi(in);
	
	if (index >= 1 && index <= 9) {
		if (board[index - 1] == " ") {
			board[index - 1] = turn;
			ret;
		}
	}
	
	doTurn(true);
}

func void gameLoop() {
	turn = "X";
	turnNum = 0;
	while (getWinner(board) == " " && turnNum < 9) {
		turnNum++;
		
		doTurn(false);
		
		if (turn == "X") {
			turn = "O";
		} else {
			turn = "X";
		}
	}
	
	auto winner = getWinner(board);
	printBoard();
	if (winner == " ") {
		println("It is a tie!");
	} else {
		println(winner + " wins!");
	}
}

gameLoop();